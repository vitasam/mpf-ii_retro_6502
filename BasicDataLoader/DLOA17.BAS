15 TEXT : HOME : REM FILE "DLOA.."
25 PRINT "DATA LOADER, VER.1.7"
30 PRINT "START SERVER,PRESS ENTER"
35 GET A$
37 AC = 0 : REM POKE ADDRESS COUNTER
40 BX = 0 : REM BYTE FROM PORT
42 CN = 0 : REM BYTES COUNTER
50 REM === GET ADDRESS
52 GOSUB 605
53 AH = S
60 GOSUB 605
63 AD = AH * 256 + S
65 PRINT "TARGET ADDRESS:";AD
70 REM === GET DATA SIZE
72 GOSUB 605
74 DH = S
76 GOSUB 605
78 DS = DH * 256 + S
79 PRINT "DATA SIZE:";DS
80 REM === GET DATA BYTES
81 AC = AD : REM POKE ADDRESS
82 FOR N = 1 TO DS
84 GOSUB 605
86 PRINT S
87 POKE AC,S: AC = AC + 1
88 NEXT N
110 PRINT : PRINT "PRESS ENT TO RUN"
112 GET A$
114 CALL AD
480 END
500 REM === GET BUSY FLAG ===
505 BX = PEEK (49168)
508 T = BX - 128
510 IF T > 0 THEN GOTO 517
512 B6 = 0 : GOTO 520
517 B6 = 1
520 RETURN
600 REM === GET BYTE VALUE ===
605 P = 7: S = 0
607 PRINT "#";CN;": ";
610 FOR V = 1 TO 8
615 L = PEEK (49241)
620 FOR Z = 1 TO 20: NEXT Z
625 L = PEEK(49240)
627 FOR Z = 1 TO 10: NEXT Z
630 GOSUB 505
632 PRINT B6;" ";
635 IF B6 > 0 THEN S = S + 2 ^ P
640 P = P - 1
645 NEXT V
646 CN = CN + 1
647 PRINT
650 RETURN 

